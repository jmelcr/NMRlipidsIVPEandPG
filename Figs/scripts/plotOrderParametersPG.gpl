set term pdfcairo enhanced rounded color dashed dashlength 1 size 10cm, 21cm #16.2cm
set out "../HGorderparametersPG.pdf"

# write name of the blog on the plot:
# set label 1 right at screen 0.982,0.995 "nmrlipids.blogspot.fi" font ",9" tc rgb "gray"
# set tmargin at screen 0.987

set tmargin at screen 0.995
set ylabel "S_{CH}" font "Arial,20" offset 2,-2.3

set bmargin at screen 0.085
set key l at screen 0.11,0.080 Left reverse noautotitle samplen 3.4 width 6 maxrows 11 font "Arial,10"
#set key below horizontal left Left reverse noautotitle samplen 1 maxcols 2 maxrows 6

set lmargin at screen 0.122 #0.157
set rmargin at screen 0.99

set xtics ("ùõΩ" 1, "ùõº" 2, "g_3" 3, "g_2" 4, "g_1" 5) offset 0,8 scale 0 font "Arial,25"
set ytics 0.1 out nomirror font "Arial,15"
set mytics 2
dx = 0.05
r = 0.8 #-- point radius
w = 3 #-- line thickness
dxl = 0.021
set bars small # no tics in the ends of error bars

# column dividers:
set arrow 1 from 1.5, graph 0 to 1.5, graph 1 nohead lw 0.5
set arrow 2 from 2.5, graph 0 to 2.5, graph 1 nohead lw 0.5
set arrow 3 from 3.5, graph 0 to 3.5, graph 1 nohead lw 0.5
set arrow 4 from 4.5, graph 0 to 4.5, graph 1 nohead lw 0.5

# Function to get horizontal lines (for experimental data):
horizLines(filename)="< awk '{ if(substr($1,1,1)!~/#/) {print $1-0.46,$2,$3;print $1+0.46,$2,$3;print n} }'".filename

# Subjective sweet spots:
set style rectangle fc rgb "#D6F0FE" fs solid noborder
set object 1 rectangle \
         from 0.0,0.05136 to 1.5,0.01136 
set object 2 rectangle \
         from 1.5,0.065376 to 2.5,0.105376
set object 3 rectangle \
         from 2.5, -0.20124 to 3.5, -0.16124
set object 4 rectangle \
         from 3.5,-0.14 to 4.5,-0.18
set object 6 rectangle \
         from 4.5,-0.09184 to 5.5,-0.13184
set object 7 rectangle \
         from 4.5,-0.02696 to 5.5,0.01304

# Line- and point types for different force fields:
# GROMOS-family:
Berger = 21
set linetype Berger lw w lc rgb "#D70039" # for Berger
set linetype 32 lw w lc rgb "#FA0059" # for Tjornhammar
CKP_QB = 25
set linetype 25 lw w lc rgb "#FFA0A3" # for CKP using the same charges as in the Berger simulations, with the NH3 group having the same charges as in the N(CH3)3 group of the PC lipids
set linetype 27 lw w lc rgb "#F70000" # for Chiu
set linetype 26 lw w lc rgb "#FF8300" # for Kukol
CKP_QK = 31
set linetype 31 lw w lc rgb "#E8007C" # for CKP using the charges for the NH3 group taken from a lysine side-chain (to be more GROMOS consistent).
# between GROMOS and CHARMM:
C36_UA = 231
set linetype C36_UA lw w lc rgb "#9966B6" # for CHARMM36-UA
# CHARMM-family:
CHARMM = 23
set linetype CHARMM lw w lc rgb "#0088BF" # for CHARMM36
# between CHARMM and AMBER:
Slipid = 22
set linetype Slipid lw w lc rgb "#7AEE9E" # "#7AEE9E" # for Slipids
set linetype 28 lw w lc rgb "#9FE100" # for HoNiLy
# AMBER-family:
Lipd17 = 241
set linetype 24 lw w lc rgb "#FFD300" # for GAFF
set linetype 241 lw w lc rgb "#FFD300" # for Amber Lipid17
# OPLS-family:
MacRog = 30
set linetype 29 lw w lc rgb "#FF578F" # for OPLS-AA
set linetype MacRog lw w lc rgb "#00A368" # for MacRog

# x-displacements to avoid ovelaps:
d=0.06
dxC36po = -7.0*d
dxL17po = -5.0*d
dxSLIdp =  0.5*d
dxSLIpo =  3.0*d




# Arrows pointing data outside the graph:
#set arrow 261 from 3.17, graph 0 to 3.17, graph -0.02 lt 26 lw 3 # Kukol g3
#
# Labels for reporting the values outside the graph:
#set label 262 at 3.22, graph 0.008 rotate by 30 "0.28" font ",9" tc rgb "#FF8300" # Kukol g3

# Explanations for superscripts:
#set label 2 at screen 0.54,0.015 \
#  "^{_{(1)}} DROSS, ^{_{(2)}} APM-CP, ^{_{(3)}} R-PDLF" font ",10"
#
# DROSS = Dipolar Recoupling On-axis with Scaling and Shape preservation
# R-PDLF = R-type Proton Detected Local Field

plot [0.5:5.5][0.2:-0.33] \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/POPG/T298K/Headgroup_Glycerol_Order_Parameters_Experiments2.dat') \
         u ($1):(($2)) w l ls 8 lc rgb "black" lw 2 t "POPG 298_{_ }K  ^{13}C NMR", \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/POPG/T298K/Headgroup_Glycerol_Order_Parameters_Experiments2.dat') \
         u ($1):(($3)) w l ls 8 lc rgb "black" lw 2, \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/POPG/T298K/Headgroup_Glycerol_Order_Parameters_Experiments_alphaAVERAGED.dat') \
         u ($1):($2*0.00784) w l ls 8 lc rgb "black" dt (7,10.45) lw 2 t "&{PO}@{\"}&{PG} &{29}@{\"}&{8_{_ }K}  ^{&{1}@{2}&{3}}H NMR", \
     "../../../MATCH/Data/Lipid_Bilayers/POPG/T298K/MODEL_CHARMM_GUI/Headgroup_Glycerol_Order_Parameters_Simulation.dat" \
     u ($1+dxC36po):($2) lt CHARMM pt 7 ps r t '&{PO}@{\"}&{PG} &{29}@{\"}&{8_{_ }K}  CHARMM36 K^+',\
     "../../../MATCH/Data/Lipid_Bilayers/POPG/T298K/MODEL_SLIPIDS/Headgroup_Glycerol_Order_Parameters_Simulation.dat" \
     u ($1+dxSLIpo):($2) lt Slipid pt 7 ps r t '&{PO}@{\"}&{PG} &{29}@{\"}&{8_{_ }K}  Slipids', \
     "../../../MATCH/Data/Lipid_Bilayers/POPG/T310K/MODEL_LIPID17_500lipids/Headgroup_Glycerol_Order_Parameters_Simulation.dat" \
     u ($1+dxL17po):($2) lt Lipd17 pt 7 ps r t '&{PO}@{\"}&{PG} 310_{_ }K  Lipid17',\
     "../../../MATCH/Data/Lipid_Bilayers/POPG/T310K/MODEL_GROMOS-CKP/OrdParmPOPGforplot.dat" \
     u ($1):($2) lt CKP_QK pt 6 ps r t '&{PO}@{\"}&{PG} &{31}@{\"}&{0_{_ }K}  GROMOS-CKP', \
     10 lc rgb "white" t "  ", \
     10 lc rgb "white" t "  ", \
     10 lc rgb "white" t "  ", \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/DPPG/T314K/Headgroup_Glycerol_Order_Parameters_Experiments.dat') \
         u ($1):($2*0.00784) w l ls 4 lc rgb "black" dt (1,6.2) lw 5 t "DPPG 314_{_ }K  ^2H NMR", \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/DPPG/T314K/Headgroup_Glycerol_Order_Parameters_Experiments.dat') \
         u ($1):($3*0.00784)  w l ls 4 lc rgb "black" dt (1,6.2) lw 5, \
     "../../../MATCH/Data/Lipid_Bilayers/DPPG/T314K/MODEL_SLIPIDS/Headgroup_Glycerol_Order_Parameters_Simulation.dat" \
     u ($1+dxSLIdp):($2) lt Slipid pt 5 ps r t '&{DP}@{\"}&{PG} &{31}@{\"}&{4_{_ }K}  Slipids', \
     10 lc rgb "white" t "  ", \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/E.coliPG/T310K/Headgroup_Glycerol_Order_Parameters_Experiments.dat') \
         u ($1):($2*0.00784) w l ls 4 lc rgb "#5F5F5F" lw 6.5 t "{/Times-Italic E. Coli} PG 310_{_ }K ^{&{1}@{2}&{3}}H NMR", \
     horizLines(' ../../../MATCH/Data/Lipid_Bilayers/E.coliPG/T310K/Headgroup_Glycerol_Order_Parameters_Experiments.dat') \
         u ($1):($3*0.00784) w l ls 4 lc rgb "#5F5F5F" lw 6.5, \
     10 lc rgb "white" t "  ", \
     10 lc rgb "white" t "  ", \
     10 lc rgb "white" t "  ", \
     10 lc rgb "white" t "  ", \
     10 lc rgb "white" t "  "

###
# Removed because there is another corresponding set, but at the correct experimental temperature:
#
#     "../../../MATCH/Data/Lipid_Bilayers/POPG/T310K/MODEL_SLIPID_500lipids/Headgroup_Glycerol_Order_Parameters_Simulation.dat" \
#     u ($1+dxSLIpo):($2) lt Slipid pt 7 ps r t 'POPG 310_{_ }K Slipids', \
#
#     "../../../MATCH/Data/Lipid_Bilayers/POPG/T310K/MODEL_CHARMM36_500lipids/Headgroup_Glycerol_Order_Parameters_Simulation.dat" \
#     u ($1+dxC36po):($2) lt CHARMM pt 7 ps r t 'POPG 310_{_ }K CHARMM36',\
#
#     "../../../MATCH/Data/Lipid_Bilayers/DPPG/T298K/MODEL_SLIPIDS/Headgroup_Glycerol_Order_Parameters_Simulation.dat" \
#     u ($1+dxSLIdp):($2) lt Slipid pt 5 ps r t 'DPPG 298_{_ }K Slipids', \
